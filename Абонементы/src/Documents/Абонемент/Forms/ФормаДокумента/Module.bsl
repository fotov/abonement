

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьОтображениеПланировщика();

КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеПланировщика()
	
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.День;
	Планировщик.КратностьПериодическогоВарианта = 7;
	ЭлементШкалыВремени = Планировщик.ШкалаВремени.Элементы.Добавить();
	ЭлементШкалыВремени.Единица = ТипЕдиницыШкалыВремени.День;
	ЭлементШкалыВремени.Кратность = 1;
	ЭлементШкалыВремени.Формат = "ДФ='dd';";
	Планировщик.ШкалаВремени.Элементы.Удалить(Планировщик.ШкалаВремени.Элементы[0]);
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	Планировщик.ОтображениеВремениЭлементов = ОтображениеВремениЭлементовПланировщика.НеОтображать;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	НачалоПериода = НачалоНедели(НачалоМесяца(ТекущаяДата()));
	КонецПериода = КонецНедели(НачалоПериода);
	Пока НачалоПериода <= КонецМесяца(ТекущаяДата()) Цикл
		Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
		НачалоПериода = КонецПериода + 1;
		КонецПериода = КонецНедели(НачалоПериода);
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИспользованиеУслуги.Ссылка,
	|	ИспользованиеУслуги.Дата
	|ИЗ
	|	Документ.ИспользованиеУслуги КАК ИспользованиеУслуги
	|ГДЕ
	|	ИспользованиеУслуги.Абонемент = &Абонемент");
	Запрос.УстановитьПараметр("Абонемент", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Элемент = Планировщик.Элементы.Добавить(НачалоДня(Выборка.Дата), КонецДня(Выборка.Дата));
		Элемент.Текст = Объект.Наименование;
		Элемент.Значение = Выборка.Ссылка.УникальныйИдентификатор();
		
	КонецЦикла;
	
КонецПроцедуры
